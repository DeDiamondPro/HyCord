plugins {
    id 'net.minecraftforge.gradle.forge' version '6f5327738d'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
}

version = mod_ver
group = mod_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = 'run'
    replace('@VER@', mod_ver)
    mappings = 'stable_22'
    makeObfSourceJar = false
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

repositories {
    mavenCentral()
    maven { url "https://repo.sk1er.club/repository/maven-public" }
    maven { url 'https://repo.spongepowered.org/maven' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    shade ('com.github.JnCrMx:discord-game-sdk4j:v0.5.4')
    shade ("gg.essential:loader-launchwrapper:1.1.2")
    implementation ("gg.essential:essential-1.8.9-forge:1566")
}

jar {
    enabled = false
    manifest.attributes(
            "ModSide": "CLIENT",
            "FMLCorePlugin": "io.github.dediamondpro.hycord.tweaker.FMLLoadingPlugin",
            "TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker",
            "TweakOrder": "0",
            "FMLCorePluginContainsFMLMod": "no yes",
            "ForceLoadAsMod": true
    )
}

shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveClassifier.set('')
    exclude 'META-INF', 'META-INF/**'
    exclude 'org/jetbrains/annotations', 'org/jetbrains/annotations/**'

    exclude "LICENSE.md"
    exclude "pack.mcmeta"
    exclude "dummyThing"
    exclude "**/module-info.class"
    exclude "*.so"
    exclude "*.dylib"
    exclude "*.dll"
    exclude "*.jnilib"
    exclude "ibxm/**"
    exclude "com/jcraft/**"
    exclude "org/lwjgl/**"
    exclude "net/java/**"

    exclude "META-INF/proguard/**"
    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"
    exclude "META-INF/com.android.tools/**"

    exclude "fabric.mod.json"
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version
    inputs.property 'name', mod_name
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand (
                'version':project.version,
                'mcversion':project.minecraft.version,
                'name':mod_name
        )
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

tasks.moveResources.dependsOn processResources
tasks.classes.dependsOn moveResources
tasks.reobfJar.dependsOn(shadowJar)
